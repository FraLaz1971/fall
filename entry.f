      REAL FUNCTION MEANMD(A,N,XMIN,XMAX)
        IMPLICIT NONE
        REAL MSUM
        INTEGER N,NUM,I
        REAL A(N),MEAN,MEANHI,MEANLO,XMIN,XMAX,AMIN,AMAX
C MEANMD CALCULATES AVERAGE OF A(I),
C WHERE XMIN.LE.A(I).LE.XMAX, FOR I=1 TO N
        AMIN=XMIN
        AMAX=XMAX
        GOTO 1
        ENTRY MEANLO(A,N,XMAX)
        WRITE(11,*) 'EXECUTED ENTRY MEANLO'
C MEANLO CALCULATES AVERAGE OF A(I),
C WHERE A(I).LE.XMAX, FOR 1=1 TO N
C THEREFORE SET AMIN TO A VERY LARGE NEGATIVE NUMBER
        AMIN=-1E30
        AMAX=XMAX
        GOTO 1
        ENTRY MEANHI(A,N,XMIN)
        WRITE(11,*) 'EXECUTED ENTRY MEANHI'
C MEANHI CALCULATES AVERAGE OF A(I),
C WHERE A(I).GE.XMIN, FOR I=1 TO N
C THEREFORE SET AMAX TO A VERY LARGE POSITIVE NUMBER
        AMIN=XMIN
        AMAX=1E30
        GOTO 1
        ENTRY MEAN(A,N)
        WRITE(11,*) 'EXECUTED ENTRY MEAN'
C MEAN CALCULATES AVERAGE OF ALL NUMBERS
C THEREFORE SET AMIN AND AMAX TO VERY LARGE NEGATIVE 
C AND POSITIVE NUMBERS
        AMIN=-1E30
        AMAX=1E30
C LIMITS SET - NOW CALCULATE AVERAGE OF NUMBERS BETWEEN
C AMIN AND AMAX
1       MSUM=0.0
        NUM=0
        DO 10,I=1,N
          IF((A(I).GE.AMIN).AND.(A(I).LE.AMAX)) THEN
            MSUM=MSUM+A(I)
            NUM=NUM+1
          END IF
10      CONTINUE
        MEAN=MSUM/NUM
        MEANLO=MEAN
        MEANHI=MEAN
        MEANMD=MEAN
        WRITE(11,*) 'MEAN COMPUTED: RETURNING ',MEAN
        RETURN
      END      
      PROGRAM UENTRY
        IMPLICIT NONE
        INTEGER N
        PARAMETER(N=11)
        REAL A(N),MEANMD,MEANLO,MEANHI,MEAN
        CHARACTER*80 OFNAM
        DATA A/10.2,-32.77,98999.12,-0.3,-789.45,-12.01,7865.0,3.12,
     & 0.12E-6,9.9999,10000.76/
        OFNAM='entry.log'
        OPEN(11,FILE=OFNAM)
C       MEANMD(A,N,XMIN,XMAX)
        PRINT *,'The meanmd is',MEANMD(A,N,-1.0E4,1.0E4)
C       MEANLO(A,N,XMAX)
        PRINT *,'The meanlo is',MEANLO(A,N,6.0E3)
C       MEANHI(A,N,XMIN)
        PRINT *,'The meanhi is',MEANHI(A,N,-7.0E2)
C       MEAN(A,N)
        PRINT *,'The mean is',MEAN(A,N)
        CLOSE(11)
      END
